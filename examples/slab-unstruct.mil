# solve a one-dimensional slab with uniform XS
# reading the mesh from a gmsh-generated file
# the grid is slightly refined toward the center
#
# run as
#  $ milonga slab.mil [ --volumes | --elements ]

# define an unstructured one-dimensional mesh
# as generated by gmsh in slab.msh (see slab.geo)
MESH FILE_PATH slab.msh DIMENSIONS 1

# define a material named fuel with the provided XS
MATERIAL fuel SigmaT 0.54628  SigmaS 0.464338  nuSigmaF 1.70*0.054628 D 1/(3*SigmaT(x))

# the mesh in slab.msh contains a named physical
# entity called "fuel" that spans the slab geometry
# as its name matches the name of a material, milonga
# automatically maps the physical entity in the mesh file
# to the material "fuel"

# set null flux at both boundaries so we can
# compare the numerical keff with the analytical one
PHYSICAL_ENTITY left  BC null
PHYSICAL_ENTITY right BC null

# advance one milonga step (i.e, solve the problem)
MILONGA_STEP

# compute the analytical keff, noting that the XS can be
# used as plain functions of x in algebraic expressinos
a = 2 * 10.371065 # critical size of the problem UD20-1-0-SL (number 22 report Los Alamos)
kdiff = nuSigmaF(0)/((SigmaT(0)-SigmaS(0))+D(0)*(pi/a)^2)

# write the results into the standard output
PRINT_FUNCTION phi1 HEADER
PRINT TEXT "\# keff       = " %.8f keff
PRINT TEXT "\# kdiff      = " %.8f kdiff
PRINT TEXT "\# rel error = " %+.2e (keff-kdiff)/keff
