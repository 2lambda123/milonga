# solve a one-dimensional slab with uniform XS
# reading the mesh from a gmsh-generated file
# the grid is slightly refined toward the center
#
# run as
#  $ milonga slab.mil [ --volumes | --elements ]

# define an unstructured one-dimensional mesh
# as generated by gmsh in slab.msh (see slab.geo)
MESH FILE_PATH slab.msh DIMENSIONS 1

# define a material named fuel with the provided XS
MATERIAL fuel D 1 nuSigmaF 2e-3 SigmaA 1e-3

# the mesh in slab.msh contains a named physical
# entity called "fuel" that spans the slab geometry
# as its name matches the name of a material, milonga
# automatically maps the physical entity in the mesh file
# to the material "fuel"

# set null flux at both boundaries so we can
# compare the numerical keff with the analytical one
PHYSICAL_ENTITY NAME left  BC null
PHYSICAL_ENTITY NAME right BC null

# advance one milonga step (i.e, solve the problem)
MILONGA_STEP

# compute the analytical keff, noting that the XS can be
# used as plain functions of x in algebraic expressinos
a = 100
kanalytical = nuSigmaF(0)/(SigmaA(0)+D(0)*(pi/a)^2)

# write the results into the standard output
PRINT_FUNCTION phi1 HEADER
PRINT TEXT "\# keff       = " %.8f keff
PRINT TEXT "\# analytical = " %.8f kanalytical
PRINT TEXT "\# rel error = " %+.2e (keff-kanalytical)/keff
